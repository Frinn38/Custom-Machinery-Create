plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false // Quiltflower, a better decompiler
    id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        mavenCentral()
        maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
        maven { url = "https://dvs1.progwml6.com/files/maven/" } // JEI
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
        maven { // Flywheel
            url = "https://maven.tterrag.com/"
            content {
                // need to be specific here due to version overlaps
                includeGroup("com.jozufozu.flywheel")
            }
        }
        maven {
            name = 'BlameJared Maven'
            url = 'https://maven.blamejared.com'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // layered mappings - Mojmap names, parchment and QM docs and parameters
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${qm_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })

        // see each subproject for dependencies.
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = "${project.archives_base_name}-${project.name}"
    version = "${project.minecraft_version}-${project.mod_version}"
    group = project.maven_group

    repositories {
        maven {
            name = "Custom Machinery maven"
            url = "https://maven.frinn.fr/repository/maven/"
            content {
                includeGroup "frinn.custommachinery"
            }
        }
        maven {
            name = "Latvian maven"
            url = "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
                includeGroup "dev.ftb.mods"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }
    if(project.path != ':common' && project.path != ':') {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    version = project.version
                    from components.java

                    pom {
                        name = project.mod_name
                        description = project.mod_description
                        packaging = 'jar'
                        version = project.version
                        scm {
                            url = project.github
                        }
                        issueManagement {
                            system = 'github'
                            url = project.issues
                        }
                        licenses {
                            license {
                                name = project.license
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = project.author
                                name = project.author
                                roles = ['developper']
                            }
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = 'maven'
                    url = 'https://maven.frinn.fr/repository/maven/'
                    credentials {
                        username = 'frinn'
                        password = System.getenv('MAVEN_PASSWORD')
                    }
                }
            }
        }
    }
}
